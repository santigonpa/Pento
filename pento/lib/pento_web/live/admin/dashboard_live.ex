defmodule PentoWeb.Admin.DashboardLive do
  use PentoWeb, :live_view

  alias PentoWeb.Endpoint
  alias PentoWeb.Admin.SurveyResultsLive
  alias PentoWeb.Admin.UserActivityLive
  alias PentoWeb.Admin.SurveyActivityLive

  @survey_results_topic "survey_results"
  @user_activity_topic "user_activity"
  @survey_activity_topic "survey_activity"

  def mount(_params, _session, socket) do
    if connected?(socket) do
      Endpoint.subscribe(@survey_results_topic)
      Endpoint.subscribe(@user_activity_topic)
      Endpoint.subscribe(@survey_activity_topic)
    end

    {:ok,
     socket
     |> assign(:survey_results_component_id, "survey_results")
     |> assign(:user_activity_component_id, "user_activity")
     |> assign(:survey_activity_component_id, "survey_activity")}
  end

  def handle_info(%{event: "rating_created"}, socket) do
    send_update(
      SurveyResultsLive,
      id: socket.assigns.survey_results_component_id
    )

    {:noreply, socket}
  end

  def handle_info(%{topic: @user_activity_topic, event: "presence_diff"}, socket) do
    send_update(
      UserActivityLive,
      id: socket.assigns.user_activity_component_id
    )

    {:noreply, socket}
  end

  def handle_info(%{topic: @survey_activity_topic, event: "presence_diff"}, socket) do
    send_update(
      SurveyActivityLive,
      id: socket.assigns.survey_activity_component_id
    )

    {:noreply, socket}
  end
end
